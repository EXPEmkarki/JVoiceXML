apply plugin: 'java'
apply plugin: "com.github.eerohele.saxon-gradle"
apply plugin: 'maven'

buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath  "gradle.plugin.com.github.eerohele:saxon-gradle:0.2.1"
    }
}

repositories {
    mavenCentral()
    jcenter()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

sourceSets {
    main {
        resources {
            exclude '**'
        }
    }
}

configurations.all {
    exclude group: 'com.spokentech', module:'speechcloud-client'
}

dependencies {
    compile group: 'net.sourceforge.halef', name: 'cairo-client', version: '0.1-SNAPSHOT'
    compile fileTree(dir: './3rdparty/speechcloud/lib', include: ['*.jar'])
    compile group: 'net.sourceforge.halef', name: 'cairo-sip', version: '0.1-SNAPSHOT'
    compile group: 'net.sourceforge.halef', name: 'cairo-rtp', version: '0.1-SNAPSHOT'
    compile group: 'net.sourceforge.halef', name: 'halef-mrcp4j', version: '0.1-SNAPSHOT'
    //compile group: 'net.sourceforge.halef', name: 'halef-zanzibar', version: '0.1-SNAPSHOT'
    compile fileTree(dir: './3rdparty/zanzibar/lib', include: ['*.jar'])
    compile project(':org.jvoicexml.interpreter.grammar.halef')
    compile group: 'javax.sip', name: 'jain-sip-api', version: '1.2.1.4'
    compile group: 'javax.sip', name: 'jain-sip-ri', version: '1.2.1'
    compile group: 'javax.sdp', name: 'nist-sdp', version: '1.0'
    compile group: 'concurrent', name: 'concurrent', version: '1.3.4'
    compile group: 'commons-lang', name: 'commons-lang', version: '2.6'
    compile group: 'javax.media', name: 'jmf', version: '2.1.1e'
    compile group: 'log4j', name: 'log4j', version: '1.2.17'
    
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.10.19'

    compile project(':org.jvoicexml')
    compile project(':org.jvoicexml.client')
}

task xsltImplementation(type: com.github.eerohele.SaxonXsltTask) {
    stylesheet "${projectDir}/src/main/resources/patch-configuration.xsl"
    input "${projectDir}/src/main/resources/mrcpv2-implementation.xml"
    output "${project(':org.jvoicexml').copyConfiguration.destinationDir}/mrcpv2-implementation.xml"
    parameters(
        buildpath: "${buildDir}/libs",
        version: version,
        libs: configurations.compile
    )
}

task xsltCallmanager(type: com.github.eerohele.SaxonXsltTask)  {
    stylesheet "${projectDir}/src/main/resources/patch-callmanager-configuration.xsl"
    input "${projectDir}/src/main/resources/mrcpv2-callmanager.xml"
    output "${project(':org.jvoicexml').copyConfiguration.destinationDir}/mrcpv2-callmanager.xml"
    parameters(
        buildpath: "${buildDir}/libs",
        version: version,
        libs: configurations.compile,
        address: "sip:127.0.0.1:4245",
        port: 4245,
        cairoAddress: "sip:cairo@speechforge.org",
        cairoHost: "127.0.0.1",
        cairoPort: 5050
    )
}

task copyReferencedLibraries(type: Copy) {
    into "$buildDir/libs"
    from(configurations.compile) {
        include 'cairo-client*.jar',
         'cairo-sip*.jar',
         'cairo-rtp*.jar',
         'halef-mrcp4j*.jar',
         'zanzibar*.jar',
         'speechcloud*.jar',
         'org.jvoicexml.interpreter.grammar.halef*.jar',
         'jain-sip-api*.jar',
         'jain-sip-ri*.jar',
         'nist-sdp*.jar',
         'concurrent*.jar',
         'commons-lang*.jar',
         'jmf*.jar',
         'log4j*.jar'
    }
}

jar {
    dependsOn xsltImplementation
    dependsOn xsltCallmanager
    dependsOn copyReferencedLibraries

    manifest {
        attributes("Implementation-Title": "JVoiceXML MRCPv2 implementation platform",
                   "Implementation-Vendor": "JVoiceXML group",
                   "Implementation-Version": version)
    }
    baseName 'org.jvoicexml.implementation.mrcpv2'
}

clean {
    delete file("${project(':org.jvoicexml').copyConfiguration.destinationDir}/mrcpv2-callmanager.xml")
    delete file("${project(':org.jvoicexml').copyConfiguration.destinationDir}/mrcpv2-implementation.xml")
}
