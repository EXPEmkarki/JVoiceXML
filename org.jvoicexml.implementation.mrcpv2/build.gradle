apply plugin: 'java'
apply plugin: "com.github.eerohele.saxon-gradle"

buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath  "gradle.plugin.com.github.eerohele:saxon-gradle:0.2.1"
    }
}

repositories {
    mavenCentral()
    jcenter()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

sourceSets {
    main {
        resources {
            exclude '**'
        }
    }
}

configurations.all {
    exclude group: 'com.spokentech', module:'speechcloud-client'
}

dependencies {
    compile group: 'log4j', name: 'log4j', version: '1.2.17'
    compile group: 'net.sourceforge.halef', name: 'cairo-client', version: '0.1-SNAPSHOT'
    compile fileTree(dir: './3rdparty/speechcloud/lib', include: ['*.jar'])
    compile group: 'net.sourceforge.halef', name: 'cairo-sip', version: '0.1-SNAPSHOT'
    compile group: 'net.sourceforge.halef', name: 'cairo-rtp', version: '0.1-SNAPSHOT'
    compile group: 'net.sourceforge.halef', name: 'halef-mrcp4j', version: '0.1-SNAPSHOT'
    compile group: 'net.sourceforge.halef', name: 'halef-zanzibar', version: '0.1-SNAPSHOT'
    
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.10.19'

    compile project(':org.jvoicexml')
    compile project(':org.jvoicexml.client')
}

xslt {
    stylesheet "${projectDir}/src/main/resources/patch-configuration.xsl"
    input "${projectDir}/src/main/resources/mrcpv2-implementation.xml"
    output "${buildDir}/config/mrcpv2-implementation.xml"
    parameters(
        buildpath: "${buildDir}/libs",
        libs: configurations.compile
    )
}

task copyReferencedLibraries(type: Copy) {
    into "$buildDir/libs"
    from(configurations.compile) {
        include 'log4j-*.jar',
         'cairo-client*.jar',
         'cairo-sipt*.jar',
         'cairo-rtp*.jar',
         'halef-mrcp4j*.jar',
         'zanzibar*.jar',
         'speechcloud*.jar'
    }
}

jar {
    dependsOn xslt
    dependsOn copyReferencedLibraries

    manifest {
        attributes("Implementation-Title": "JVoiceXML MRCPv2 implementation platform",
                   "Implementation-Vendor": "JVoiceXML group",
                   "Implementation-Version": version)
    }
    baseName 'org.jvoicexml.implementation.mrcpv2'
}