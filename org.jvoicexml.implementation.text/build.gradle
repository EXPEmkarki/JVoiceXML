apply plugin: 'java'
apply plugin: "com.github.eerohele.saxon-gradle"

buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath  "gradle.plugin.com.github.eerohele:saxon-gradle:0.2.1"
    }
}

repositories {
    mavenCentral()
}


sourceSets {
    main {
        resources {
            exclude '**'
        }
    }
}

configurations {
    jvxmlconfigure {
        description = "configure JVoiceXML plugin"
        transitive = false
    }
    
}

dependencies {
    compile group: 'log4j', name: 'log4j', version: '1.2.17'
    compile group: 'com.google.protobuf', name: 'protobuf-java', version: '3.1.0'
    
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.10.19'

    compile project(':org.jvoicexml')
    compile project(':org.jvoicexml.client')
    compile project(':org.jvoicexml.client.text')
}

xslt {
    stylesheet "${projectDir}/src/main/resources/patch-configuration.xsl"
    input "${projectDir}/src/main/resources/text-implementation.xml"
    output "${buildDir}/config/text-implementation.xml"
    parameters(
        repositoryname: "text",
        buildpath: "${buildDir}/libs",
        libs: configurations.compile,
        port: "1099"
    )
}

task copyReferencedLibraries(type: Copy) {
    into "$buildDir/libs"
    from(configurations.compile) {
        include 'log4j-*.jar', 'protobuf-java*.jar'
    }
}

jar {
    dependsOn xslt
    dependsOn copyReferencedLibraries

    manifest {
        attributes("Implementation-Title": "JVoiceXML text implementation platform",
                   "Implementation-Vendor": "JVoiceXML group",
                   "Implementation-Version": "version")
    }
    baseName 'org.jvoicexml.implementation.text'
}