apply plugin: 'java'
apply plugin: "com.github.eerohele.saxon-gradle"

buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath  "gradle.plugin.com.github.eerohele:saxon-gradle:0.2.1"
    }
}

repositories {
    mavenCentral()
}


sourceSets {
    main {
        resources {
            exclude '**'
        }
    }
}

dependencies {
    compile group: 'com.google.protobuf', name: 'protobuf-java', version: '3.1.0'
    
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.10.19'

    compile project(':org.jvoicexml')
    compile project(':org.jvoicexml.client')
    compile project(':org.jvoicexml.client.text')
    compile project(':org.jvoicexml.interpreter.grammar.luis')
    compile project(':org.jvoicexml.srgs')
}

xslt {
    stylesheet "${projectDir}/src/main/resources/patch-configuration.xsl"
    input "${projectDir}/src/main/resources/text-implementation.xml"
    output "${project(':org.jvoicexml').copyConfiguration.destinationDir}/text-implementation.xml"
    parameters(
        buildpath: "${buildDir}/libs",
        version: version,
        subscriptionId: project.property('org.jvoicexml.demo.luis.applicationId'),
        libs: configurations.compile
    )
}

task copyReferencedLibraries(type: Copy) {
    into "$buildDir/libs"
    from(configurations.compile) {
        include 'protobuf-java*.jar',
        'org.jvoicexml.client.text*.jar',
        'org.jvoicexml.srgs*.jar',
        'org.jvoicexml.interpreter.grammar.luis*.jar'
    }
}

jar {
    dependsOn xslt
    dependsOn copyReferencedLibraries

    manifest {
        attributes("Implementation-Title": "JVoiceXML text implementation platform",
                   "Implementation-Vendor": "JVoiceXML group",
                   "Implementation-Version": "version")
    }
    baseName 'org.jvoicexml.implementation.text'
}

clean {
    delete file("${project(':org.jvoicexml').copyConfiguration.destinationDir}/text-implementation.xml")
}
