apply plugin: 'java'
apply plugin: "com.github.eerohele.saxon-gradle"

buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath  "gradle.plugin.com.github.eerohele:saxon-gradle:0.2.1"
    }
}

repositories {
    mavenCentral()
    jcenter()
}

sourceSets {
    main {
        resources {
            exclude '**'
        }
    }
}

dependencies {
    compile group: 'log4j', name: 'log4j', version: '1.2.17'
    compile fileTree(dir: './3rdparty/mary5.1/lib', include: ['*.jar'])
    //compile group: 'de.dfki.mary', name: 'voice-cmu-slt-hsmm', version: '5.2'
    
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.10.19'

    compile project(':org.jvoicexml')
}

xslt {
    stylesheet "${projectDir}/src/main/resources/patch-configuration.xsl"
    input "${projectDir}/src/main/resources/mary-implementation.xml"
    output "${project(':org.jvoicexml').copyConfiguration.destinationDir}/mary-implementation.xml"
    parameters(
        buildpath: "${buildDir}/libs",
        libs: configurations.compile
    )
}

task copyReferencedLibraries(type: Copy) {
    into "$buildDir/libs"
    from(configurations.compile) {
        include 'log4j-*.jar', 'marytts-client*.jar'
    }
}

jar {
    dependsOn xslt
    dependsOn copyReferencedLibraries

    manifest {
        attributes("Implementation-Title": "JVoiceXML mary implementation platform",
                   "Implementation-Vendor": "JVoiceXML group",
                   "Implementation-Version": version)
    }
    baseName 'org.jvoicexml.implementation.mary'
}