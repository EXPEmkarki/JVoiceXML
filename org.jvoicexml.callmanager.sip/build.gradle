apply plugin: 'java'
apply plugin: "com.github.eerohele.saxon-gradle"

buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath  "gradle.plugin.com.github.eerohele:saxon-gradle:0.2.1"
    }
}

repositories {
    mavenCentral()
}

sourceSets {
    main {
        resources {
            exclude '**'
        }
    }
}

dependencies {
    compile group: 'log4j', name: 'log4j', version: '1.2.17'
    compile group: 'javax.sip', name: 'jain-sip-api', version: '1.2.1.4'
    compile group: 'javax.sdp', name: 'nist-sdp', version: '1.0'
        
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.10.19'

    compile project(':org.jvoicexml')
    compile project(':org.jvoicexml.client')
}

xslt {
    stylesheet "${projectDir}/src/main/resources/patch-configuration.xsl"
    input "${projectDir}/src/main/resources/sip-callmanager.xml"
    output "${buildDir}/config/sip-callmanager.xml"
    parameters(
        buildpath: "${buildDir}/libs",
        libs: configurations.compile
    )
}

task copyReferencedLibraries(type: Copy) {
    into "$buildDir/libs"
    from(configurations.compile) {
        include 'log4j-*.jar',
        'jain-sip-api*.jar',
        'nist-sdp*.jar'
    }
}

jar {
    dependsOn xslt
    dependsOn copyReferencedLibraries

    manifest {
        attributes("Implementation-Title": "JVoiceXML SIP call manager",
                   "Implementation-Vendor": "JVoiceXML group",
                   "Implementation-Version": version)
    }
    baseName 'org.jvoicexml.callmanager.sip'
}