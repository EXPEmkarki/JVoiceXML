buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath  "gradle.plugin.com.github.eerohele:saxon-gradle:0.2.1"
    }
}

apply plugin: 'java'
apply plugin: "com.github.eerohele.saxon-gradle"
apply plugin: 'maven'

repositories {
    mavenLocal()
    mavenCentral()
    maven {
            url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

sourceSets {
    main {
        resources {
            exclude '**'
        }
    }
}

dependencies {
    compile fileTree(dir: "${project(':org.jvoicexml').projectDir}/3rdparty/freetts1.2/lib", include: ['*.jar'])
    compile group: 'org.jvoicexml', name: 'javax.speech', version: '0.5.1-SNAPSHOT'
    compile group: 'org.jvoicexml', name: 'org.jvoicexml.jsapi2', version: '0.5.1-SNAPSHOT'
    compile group: 'org.jvoicexml', name: 'org.jvoicexml.jsapi2.freetts', version: '0.5.1-SNAPSHOT'
    compile group: 'org.jvoicexml', name: 'org.jvoicexml.jsapi2.sphinx4', version: '0.5.1-SNAPSHOT'
    
    //compile fileTree(dir: './3rdparty/jsapi2.0/lib', include: ['*.jar'])
    
    testCompile fileTree(dir: "${project(':org.jvoicexml').projectDir}/3rdparty/sphinx4/lib", include: ['*.jar'])
    
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.10.19'

    compile project(':org.jvoicexml')
    compile project(':org.jvoicexml.client')
}

xslt {
    stylesheet "${projectDir}/src/main/resources/patch-configuration.xsl"
    input "${projectDir}/src/main/resources/jsapi20-implementation.xml"
    output "${project(':org.jvoicexml').copyConfiguration.destinationDir}/jsapi20-implementation.xml"
    parameters(
        buildpath: "${buildDir}/libs",
        version: version,
        libs: configurations.compile
    )
}

task copyReferencedLibraries(type: Copy) {
    into "$buildDir/libs"
    from(configurations.compile) {
         include 'freetts*.jar',
         'cmu_us_kal*.jar',
         'cmulex*.jar',
         'en_us*.jar',
         'javax.speech*.jar',
         'org.jvoicexml.jsapi2*.jar'
    }
}

jar {
    dependsOn xslt
    dependsOn copyReferencedLibraries

    manifest {
        attributes("Implementation-Title": "JVoiceXML JSAPI 2.0 implementation platform",
                   "Implementation-Vendor": "JVoiceXML group",
                   "Implementation-Version": version)
    }
    baseName 'org.jvoicexml.implementation.jsapi20'
}

clean {
    delete file("${project(':org.jvoicexml').copyConfiguration.destinationDir}/jsapi20-implementation.xml")
}
